COLOR := "\\033[0;34m"
NO_COLOR := "\\033[0m"

# Show all available recipes
@_default:
    printf "\n{{ COLOR }}# Vale (path: \`.vale/\`)\n"
    printf "===================={{ NO_COLOR }}\n"
    just --list --unsorted

# Build a local version of `openverse-vale:local` for testing
build:
    docker build . -t openverse-vale:local

# 1. List all MD and MDX files in the repository
# 2. Filter out the directories we do not want to lint
# 3. Strip the absolute path from the beginning of the files
# 4. Replace newlines with spaces
FILES := ```
    find $PWD/.. -type f \( -name "*.md" -o -name "*.mdx" \) \
    | grep -vE ".pnpm|changelogs|projects/proposals" \
    | sed "s|$PWD/../||" \
    | tr '\n' ' '
```

# Print the list of FILES for Vale to lint
debug:
    echo {{ FILES }} | tr ' ' '\n'

# Run Vale configured for Openverse in Docker.
# Using Docker avoids the need for contributors to install the Vale binary.
#Configuration defaults to what is used for CI.
run +files=FILES: build
    docker run --rm \
        -v $PWD/..:/src:rw,Z \
        --workdir=/src \
        openverse-vale:local \
        {{ files }}
